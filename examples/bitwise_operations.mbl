..

# shift left with overflow out only
:Sl
}0 }0
<< ^7
{0 {<

# shift left with overflow in only
:Shli
}0 ..
<< }1
{0 //

# shift left with overflow in and out
:Shlo
.. }0 ..
{< Sl }1
.. {0 //

# shift right with overflow out only
:Sr
}0 }0 80
>> ^7 &0
{0 =1 {>
.. &0 ..
.. \/ ..

# shift right with overflow in only
:Shri
.. }1
}0 >>
\\ {1

# shift right with overflow in and out
:Shro
.. }1 }1 80
}0 >> ^7 &0
\\ {1 =1 {>
.. .. &0 ..
.. .. \/ ..

# extract the nth bit from a byte
:Bitx
# }0 is the byte, }1 is the bit index, 0 is LSB
}0 }0 }0 }0 }0 }0 }0 }0
&0 &1 &2 &3 &4 &5 &6 &7
^0 ^1 ^2 ^3 ^4 ^5 ^6 ^7
{0 {0 {0 {0 {0 {0 {0 {0
}1 }1 }1 }1 }1 }1 }1 }1
=0 =1 =2 =3 =4 =5 =6 =7
&0 &1 &2 &3 &4 &5 &6 &7

:Bdif
# returns the amount of bits that differ between two marbles
# 0 means equality
}0 }1 }0 }1 }0 }1 }0 }1 }0 }1 }0 }1 }0 }1 }0 }1
^0 ^0 ^1 ^1 ^2 ^2 ^3 ^3 ^4 ^4 ^5 ^5 ^6 ^6 ^7 ^7 # mask out individual bits of }0 and }1
Xo rr Xo rr Xo rr Xo rr Xo rr Xo rr Xo rr Xo rr # feed them into Xorr
\\ @0 // @0 \\ .. // .. \\ .. // @1 \\ @1 // .. # add the results together
.. .. .. \\ @2 // @2 .. @3 \\ @3 // .. .. .. ..
.. .. .. .. .. .. \\ {0 // .. .. .. .. .. .. ..

#include logical_operations.mbl