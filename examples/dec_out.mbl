# prints a marble as three decimal digits
I0
Dp

# prints one marble as three decimal digits
:Dp
I0 .. ..
De co ut
+O +O +O
+O +O +O

# returns three decimal digits representing one input marble
:Decout
I0 I0 I0
/A /A %A
/A %A ..
O0 O1 O2

# divide by ten
# http://stackoverflow.com/a/19076173/13675
:/A
.. I0 .. I0 .. # right I0 is used for calculating r at the bottom
.. >> I0 .. .. 
.. >> >> \\ .. # q = (I0 >> 1) + (I0 >> 2)
.. .. // .. //
.. /\ >> \\ ..
.. .. >> .. //
.. .. >> \\ ..
.. .. >> .. ..
\\ >> // .. .. # q = q + (q >> 4)
.. >> .. .. ..
.. >> .. .. ..
.. /\ .. .. .. # q = q >> 3
.. .. /\ << ..
.. .. .. << ..
.. \\ << // .. # q = (q + (q << 2)) << 1
.. .. !! .. .. 
.. .. ++ .. .. # !! and ++ make unary negation
.. .. \\ .. // # r = -q + I0
\\ .. .. <A \/
.. \\ .. R0 ..
.. .. .. ++ ..
.. .. \\ .. .. # q = q + (r>9)
.. .. .. O0 .. # O0 = q

# modulo ten
:%A
P0 ..
I0 ..
>9 -A
O0 P0
